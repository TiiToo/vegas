/*

  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at 
  
           http://www.mozilla.org/MPL/ 
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the License. 
  
  The Original Code is Vegas Framework.
  
  The Initial Developer of the Original Code is
  ALCARAZ Marc (aka eKameleon)  <vegas@ekameleon.net>.
  Portions created by the Initial Developer are Copyright (C) 2004-2005
  the Initial Developer. All Rights Reserved.
  
  Contributor(s) :
  
*/

/** EventDispatcher

	AUTHOR
		Name : EventDispatcher
		type : SSAS
		Package : vegas.events
		Version : 1.0.0.0
		Author : ekameleon
		Date : 2006-05-27
		URL : http://www.ekameleon.net
		Mail : vegas@ekameleon.net
	
	METHOD SUMMARY
	
	CONSTRUCTOR
	
		new EventDispatcher(target:IEventDispatcher) ;

	USE
	
		BasicEvent = vegas.events.BasicEvent ;
		Delegate = vegas.events.Delegate ;
		EventDispatcher = vegas.events.EventDispatcher ;
		
		var EVENT_TYPE = "onTest" ;
		
		var action = function ( e ) {
		
			if( e != undefined) {
				trace ("> action : " + e.getType()) ;
			} else {
				trace ("> action : no event") ;
			}
			
		}
		
		var EVENT_TYPE = "onEVENT" ;
		
		var listener = new Delegate(this, action) ;
		
		var dispatcher = new EventDispatcher() ;
		dispatcher.addEventListener( EVENT_TYPE , listener ) ;
		
		var e = new BasicEvent( EVENT_TYPE , this ) ;
		dispatcher.dispatchEvent(e) ;
	
	PROPERTY SUMMARY
	
		- parent:EventDispatcher

	METHOD SUMMARY
	
		- addChild(child:EventDispatcher):Void
		
		- addEventListener(eventName:String, listener:EventListener, useCapture:Boolean, priority:Number, autoRemove:Boolean)
		
			PARAMS
			
				- eventName:String
					
					String : corresponding of the event type's name
				
					PS : if eventName value is "ALL" addEventListener use addGlobalListener
				
				- listener:EventListener
					
					EventListener object
				
				- useCapture:Boolean
				
				- priority:Number
				
					Determines the priority level of the event listener.
					
				- autoRemove:Boolean
				
					Apply a removeEventListener after the first trigger		
		
		- addGlobalEventListener(listener:EventListener, priority:Number, autoRemove:Boolean)
		
			DESCRIPTION 
			
				apply a global broadcast 
		

		- dispatchEvent( event , [isQueue], [target], [context])
			
			DESCRIPTION
				
				dispatch event
			
			PARAMS
				
				- event : 
				
					- a string 
					- a Event Object
					- a basic object with properties
						- type [obligatory]
						- target [optional]
						- context [optional]
				
				- isQueue [Boolean] [optional]
				
				- target [optional]
				
				- context [optional]
			
			RETURNS 
			
				an Event object or null

		- static flush():Void 
		
			remove all singleton instances
		
		- getEventListeners(eventName:String) : EventListenerCollection
		
			return an EventListenerCollection object for a specific type of event.

		- getGlobalEventListeners()
		
			return a EventListenerCollection object for global events listeners.

		- static getInstance(name:String):EventDispatcher

		- getRegisteredEventNames()
			
			return a HashSet object
	
		- hasEventListener(eventName:String)
		
			Checks whether the EventDispatcher object has any listeners registered for a specific type of event.
			This allows you to determine where altered handling of an event type has been introduced in the event flow heirarchy by an EventDispatcher object.
			To determine whether a specific event type will actually trigger an event listener, use EventDispatcher.willTrigger(). 
		
		- static release(name:String):EventDispatcher
		
		- removeChild(child:EventDispatcher):Void
		
		- removeEventListener(eventName:String, listener )
		
			Removes a listener from the EventDispatcher object.
			If there is no matching listener registered with the EventDispatcher object, then calling this method has no effect. 
		
			Parameters :
			
				- eventName :String -> Specifies the type of event.
				
				- listener -> the class name(string) or a EventListener object.
				
		
		- removeGlobalEventListener( listener ) 

			Removes all "Global" listeners from the EventDispatcher object.

		- static removeInstance(name:String):Boolean
		
		- toString():String

	INHERIT
	
		CoreObject > EventDispatcher

	IMPLEMENTS 
	
		EventTarget, IEventDispatcher, IFormattable, IHashable

	THANKS
	
		EventDispatcher is a AS2 port of the Java.schst.net EventDispatcher. Inspired by the NotificationCenter of Apple's Cocoa-Framework.
		
			- EventDispatcher JAVA : Stephan Schmid - http://schst.net/
			- Cocoa-Framework : http://developer.apple.com/cocoa/
			- Notification center : http://developer.apple.com/documentation/Cocoa/Conceptual/Notifications/index.html

	INHERIT

	IMPLEMENTS
	
		EventTarget â†’ IEventDispatcher	
	
**/ 
if (vegas.events.EventDispatcher == undefined) {

	// ----o Import

	require("vegas.events.EventListenerCollection") ;
	require("vegas.events.EventListenerComparator") ;
	require("vegas.events.EventListenerContainer") ;
	require("vegas.events.EventQueue") ;
	require("vegas.events.IEventDispatcher") ;
	
	// ----o Constructor

	vegas.events.EventDispatcher = function ( target /*IEventDispatcher*/ , parent /*EventDispatcher*/ ) 
	{ 
		
		this._globalListeners = new vegas.events.EventListenerCollection() ;
		
		this._captures = new vegas.data.map.HashMap() ;
		
		this._listeners = new vegas.data.map.HashMap() ;
		
		this._queue = new vegas.events.EventQueue() ;
		
		this._target = target ;
		
		/*
		trace(this + " >> " + this._target) 
		if (this._target != null ) trace(this._target.getConstructorPath())
		*/
		
		this.parent = parent ;
		
	}

	// ----o Inherit
	
	vegas.events.EventDispatcher.extend(vegas.events.IEventDispatcher) ;
	
	// ----o Public Properties
	
	vegas.events.EventDispatcher.prototype.parent = null ; /*EventDispatcher*/
	
	// ----o Private Properties
	
	vegas.events.EventDispatcher.instances /*HashMap*/ = new vegas.data.map.HashMap() ;
	
	vegas.events.EventDispatcher.prototype._globalListeners /*EventListenerCollection*/ = null ;
	vegas.events.EventDispatcher.prototype._captures /*HashMap*/ = null ;
	vegas.events.EventDispatcher.prototype._listeners /*HashMap*/ = null ;
	vegas.events.EventDispatcher.prototype._queue /*EventQueue*/ = null ;
	vegas.events.EventDispatcher.prototype._target /*IEventDispatcher*/ = null ;
	
	// ----o Public Methods
	
	vegas.events.EventDispatcher.prototype.addChild = function ( child /*EventDispatcher*/ ) 
	{
		child.parent = this ;
	}

		
	vegas.events.EventDispatcher.prototype.addEventListener = function ( eventName/*String*/, listener/*EventListener*/, useCapture/*Boolean*/, priority/*Number*/, autoRemove/*Boolean*/) /*Void*/ 
	{
		
		priority = isNaN(priority) ? 0 : priority ;
		
		if (eventName == "ALL") {
			
			this.addGlobalEventListener(listener, priority, autoRemove) ;
			
		} else {
			
			var map /*HashMap*/ = (!useCapture) ? this._listeners : this._captures ;
			
			if ( !map.containsKey(eventName)) {
				map.put( eventName, new vegas.events.EventListenerCollection() ) ;
			}
			
			var col /*EventListenerCollection*/ = map.get(eventName) ;
			col.addListener(listener, autoRemove, priority) ;	
			
			this._dispatchQueuedEvents() ;
			
		}
		
	}

	vegas.events.EventDispatcher.prototype.addGlobalEventListener = function(listener/*EventListener*/, priority/*Number*/, autoRemove/*Boolean*/) /*Void*/ 
	{
		this._globalListeners.addListener(listener, autoRemove, priority ) ;
        this._dispatchQueuedEvents() ;
	}

	vegas.events.EventDispatcher.prototype.dispatchEvent = function (event, isQueue/*Boolean*/, target, context) /*Event*/ 
	{
		
		if (!event) return null ;
	
		var e /*Event*/ = vegas.util.factory.EventFactory.create(event, (target || this), context) ;
		
		if ( e == null) return null ;
		
		var phase /*Number*/ = e.getEventPhase() ;
		
		var EventPhase = vegas.events.EventPhase ;
		
		if (phase == EventPhase.AT_TARGET) 
		{
		
			this._capture(e) ; // CAPTURING_PHASE

			e.setEventPhase( EventPhase.AT_TARGET ) ;
			
			e.setCurrentTarget(this) ;
			
			this._propagate(e, isQueue || false ) ; // AT_TARGET
			
			this._bubble(e) ; // BUBBLING_PHASE
			
			e.setEventPhase( EventPhase.AT_TARGET ) ; // TODO inclure cette initialisation dans initEvent ??
			
		}
		else if (phase == EventPhase.BUBBLING_PHASE) 
		{
			this._propagateBubble(e) ;
		}
		else if (phase == EventPhase.CAPTURING_PHASE) 
		{
			
			this._propagateCapture(e) ;
			
		}
		return e ;
		
	}
	
	vegas.events.EventDispatcher.flush = function () /*Void*/ 
	{
		
		vegas.events.EventDispatcher.instances.clear() ;
		
	}

	vegas.events.EventDispatcher.prototype.getEventListeners = function(eventName/*String*/)/*EventListenerCollection*/ 
	{
		
		if ( this._listeners.containsKey(eventName) ) return this._listeners.get(eventName) ;
		
		return new vegas.events.EventListenerCollection() ;
		
	}
	
	vegas.events.EventDispatcher.prototype.getGlobalEventListeners = function()/*EventListenerCollection*/ 
	{
		
		return this._globalListeners ;
		
	}

	vegas.events.EventDispatcher.getInstance = function ( name /*String*/ ) /*EventDispatcher*/ 
	{
		var EventDispatcher = vegas.events.EventDispatcher ;
		if (!name) name = "__default__" ;
		if (! EventDispatcher.instances.containsKey(name) ) {
			EventDispatcher.instances.put(name, new EventDispatcher()) ;
		}
		return EventDispatcher.instances.get(name) ;
	}

	vegas.events.EventDispatcher.prototype.getRegisteredEventNames = function () /*Set*/ 
	{
		
    	return new vegas.data.set.HashSet( this._listeners.getKeys() ) ;
		
    }

	vegas.events.EventDispatcher.prototype.getTarget = function () 
	{
		return this._target != null ? this._target : this ;	
	}

	vegas.events.EventDispatcher.prototype.hasEventListener = function(eventName/*String*/)/*Boolean*/ 
	{
		return this._listeners.containsKey(eventName) ;
	}

	vegas.events.EventDispatcher.release = function (name /*String*/ ) /*EventDispatcher*/ 
	{
		if (!name) name = "__default__" ;
		return vegas.events.EventDispatcher.instances.remove(name) ;
	}

	vegas.events.EventDispatcher.prototype.removeChild = function ( child /*EventDispatcher*/ ) /*Void*/ 
	{
		child.parent = null ;
	}

	vegas.events.EventDispatcher.prototype.removeEventListener = function (eventName/*String*/, listener, useCapture/*Boolean*/)/*EventListener*/ 
	{
		
		if (eventName == "ALL") {
			return this.removeGlobalEventListener(listener) ;
		}
		
		if (
			(listener instanceof vegas.events.EventListener) 
				|| vegas.util.TypeUtil.typesMatch(listener, String)
					|| (listener.handleEvent != null)
		)  
			{
			
			var map /*HashMap*/ = (!useCapture) ? this._listeners : this._captures ;
			
			if (! map.containsKey(eventName) ) {
				return null ;
			}
			
			var col /*EventListenerCollection*/ = map.get(eventName) ;
			
			var container /*EventListenerContainer*/ = col.removeListener(listener) ;
			
			if (container != null) 
				{
				return container.getListener() ;
				}
			
			}
		
        return null ;
		
	}

	vegas.events.EventDispatcher.prototype.removeGlobalEventListener = function( listener )/*EventListener*/ 
	{
		
		if (  
		
			(listener instanceof vegas.events.EventListener) 
				|| ( typeof(listener) == "string" ) 
					|| (listener.handleEvent != null)
		)
			{
			
			var container /*EventListenerContainer*/ = this._globalListeners.removeListener(listener) ;
			
			if (container != null) 
				{
				return container.getListener() ;
				}
			}
			
			return null ;
			
			}

	vegas.events.EventDispatcher.removeInstance = function (name /*String*/ ) /*Boolean*/ 
	{
		if ( vegas.events.EventDispatcher.instances.containsKey(name) ) {
			return vegas.events.EventDispatcher.instances.remove(name) != null ;
		} else {
			return false ;
		}
	}
	
	// ----o Private Properties
	
	vegas.events.EventDispatcher.prototype._globalListeners = null ;

	// ----o Private Methods


	vegas.events.EventDispatcher.prototype._bubble = function (e /*Event*/ ) /*Boolean*/ {
	
		var EventPhase = vegas.events.EventPhase ;
		
		
		if (e["stop"] >= EventPhase.STOP ) {
			
			return false ;
			
		}
		
		var parents /*Array*/ = this._getParents() ;
		
		if (parents != null) {
			
			var l /*Number*/ = parents.length ;
			
			var i /*Number*/ = 0 ;
			
			var current /*EventDispatcher*/ ;
			
			while (i<l) {
				if (e.getBubbles()) {
					
					current = parents[i] ;
					
					e.setCurrentTarget(current.getTarget()) ;
					
					e.setEventPhase( EventPhase.BUBBLING_PHASE ) ;
					
					current.dispatchEvent(e) ;
					
					if (e["stop"] >= EventPhase.STOP ) {
						return false ;
					}
					
				}
				i++ ;
			}
		}
		return true ;
	}
	
	vegas.events.EventDispatcher.prototype._capture = function( e /*Event*/ ) /*Boolean*/ {
		
		var parents /*Array*/ = this._getParents() ;
		
		if (parents != null) {
			
			var l /*Number*/ = parents.length ;
			
			var current /*EventDispatcher*/ ;
				
			var EventPhase = vegas.events.EventPhase ;
			
			while (--l > -1) {
				
				current = parents[l] ;
				
				e.setCurrentTarget( current.getTarget() ) ;
				
				e.setEventPhase(EventPhase.CAPTURING_PHASE) ;
				
				current.dispatchEvent(e) ;
				
				if (e["stop"] >= EventPhase.STOP ) {
					return false ;
				}
				
			}
		}
		
		return true ;
		
	}
	
	vegas.events.EventDispatcher.prototype._dispatchQueuedEvents = function() /*Void*/ {
		
		var q /*Queue*/ = this._queue.getQueuedEvents();
		
		if (q.size() > 0) {
			
			var ar /*Array*/ = q.toArray() ;
			
			var len /*Number*/ = ar.length ;
			
			for (var i /*Number*/ = 0 ; i<len ; i++) {
				
				var e /*Event*/ = ar[i] ;
				
				this.dispatchEvent(e, e.isQueued()) ;
				
			}
			
		}
		
	}

	vegas.events.EventDispatcher.prototype._getParents = function () /*Array*/ {
		
		if (this.parent == null) return null ;
		
		var ar /*Array*/ = [] ;
		
		var tmp /*EventDispatcher*/ = this.parent ;
		
		while(tmp != null) {
			
			ar.push(tmp) ;
			tmp = tmp.parent ;
			
		}
		
		return ar ;
		
	}

	vegas.events.EventDispatcher.prototype._propagateBubble = function ( e /*Event*/) /*Void*/ {
		
		var EventPhase = vegas.events.EventPhase ;
		
		if ( e.getEventPhase(EventPhase.BUBBLING_PHASE) ) {
			
			e.setCurrentTarget( this.getTarget() ) ;
			this._propagate(e) ;
			
		}
		
	}
	
	vegas.events.EventDispatcher.prototype._propagateCapture = function ( e /*Event*/ ) /*Void*/ {
		
		var EventPhase = vegas.events.EventPhase ;
		
		if ( this._captures.containsKey( e.getType() ) ) {
			
			e.getEventPhase(EventPhase.CAPTURING_PHASE) ;
            
            var col/*EventListenerCollection*/ = this._captures.get( e.getType() ) ;
            
            col.propagate(e) ;
            
        }
        
	}
	
	vegas.events.EventDispatcher.prototype._propagate = function ( e /*Event*/, isQueue /*Boolean*/ ) /*Event*/ {
	
		var EventPhase = vegas.events.EventPhase ;
		
		if (e["stop"] >= EventPhase.STOP ) 
		{
			
			return e ; // hack the interface limitation
			
		}
		
		if ( this._listeners.containsKey(e.getType()) ) 
		{
            
            var col /*EventListenerCollection*/ = this._listeners.get(e.getType()) ;
            col.propagate(e) ;
            
        }
        
        if ( e.isCancelled() ) 
        {
			
			return e ;
			
		}
        
        this._globalListeners.propagate(e) ;
        
        if (isQueue == false || e.isCancelled()) 
        {
			return e ;
		}
		
        this._queue.enqueue(e) ;
        
        return e ;
        
	}
	
	// ----o Encapsulate

	//trace ("***** running vegas.events.EventDispatcher") ;
	
}